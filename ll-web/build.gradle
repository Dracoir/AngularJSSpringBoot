/*
 * Main build file for the Spring Boot Lunch and learn Web component
 *
 * This simple project represents a controller layer
 * 
 */
buildscript {
    ext {
        springBootVersion = '1.2.5.RELEASE'
		wrapperVersion = '2.3'
		warGroupId = 'com.ricardorodriguezfl.lunchnlearn'
		warArtifactId = 'll-web'
		warVersion = '0.0.1'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
        classpath("io.spring.gradle:dependency-management-plugin:0.5.1.RELEASE")
		classpath("net.saliman:gradle-cobertura-plugin:2.2.7")
    }
}

//---------------------------------------------------------------------------------------------------------------------------------
apply plugin: 'java'
//apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'spring-boot' 
apply plugin: 'war'
apply plugin: 'project-report'
apply plugin: 'net.saliman.cobertura'
apply plugin: 'io.spring.dependency-management'

//---------------------------------------------------------------------------------------------------------------------------------
sourceCompatibility = 1.8
targetCompatibility = 1.8

//---------------------------------------------------------------------------------------------------------------------------------
repositories {
	mavenLocal()
    mavenCentral()
}

//---------------------------------------------------------------------------------------------------------------------------------
configurations {
    providedRuntime
}

//---------------------------------------------------------------------------------------------------------------------------------
dependencies {
    compile("org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}") // Adds additional, useful endpoints
	compile("org.springframework.boot:spring-boot-starter-aop:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-jersey")
	compile("com.ricardorodriguezfl.lunchnlearn:ll-model:0.0.1")
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat:${springBootVersion}")
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
}

//---------------------------------------------------------------------------------------------------------------------------------
// NOTE: Uncomment if you are using the Eclipse IDE, as well as the Eclipse plugin.  Don't forget to comment the IDEA plugin if you
//       use Eclipse
//eclipse {
//    classpath {
//         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
//         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
//    }
//}

//---------------------------------------------------------------------------------------------------------------------------------
install {
	repositories.mavenInstaller {
		pom.project {
			parent {
			  groupId 'org.springframework.boot'
			  artifactId 'spring-boot-starter-parent'
			  version "${springBootVersion}"
			}
		}
		pom.groupId = warGroupId
		pom.artifactId = warArtifactId
		pom.version = warVersion
	}
}

//---------------------------------------------------------------------------------------------------------------------------------
war {
	manifest {
		attributes("Implementation-Title": project.name,
				   "Implementation-Version": warVersion,
				   "Created-By": "Ricardo Rodriguez",
				   "Class-Path": configurations.compile.collect { it.getName() }.join(' '))
	}
    baseName = warArtifactId
    version = warVersion
}

//---------------------------------------------------------------------------------------------------------------------------------
task wrapper(type: Wrapper) {
	description 'Generates Gradle wrapper for project, version 2.3'
    gradleVersion = '2.3'
}

//-------------------------------------------------------------------------------------------------------------------------
// Tasks exexuted when Gradle is not given a specific task to run
defaultTasks 'clean', 'build', 'bootRun'